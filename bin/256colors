#!/bin/zsh

# TAKEN FROM 256colors.pl:
#
# Author: Todd Larason <jtl@molehill.org>
# $XFree86: xc/programs/xterm/vttests/256colors2.pl,v 1.1 1999/07/11 08:49:54 dawes Exp $

# use the resources for colors 0-15 - usually more-or-less a
# reproduction of the standard ANSI colors, but possibly more
# pleasing shades

if [[ "$1" == "set-colors" ]]; then

    # colors 16-231 are a 6x6x6 color cube
    integer red; integer green; integer blue;
    for ((red = 0; $red < 6; red = $red + 1)) {
        for ((green = 0; $green < 6; green = $green + 1)) {
            for ((blue = 0; $blue < 6; blue = $blue + 1)) {
                print -n -f "\x1b]4;%d;rgb:%2.2x/%2.2x/%2.2x\x1b\\" \
                    $(( 16 + ($red * 36) + ($green * 6) + $blue )) \
                    $(($red * 42.5)) \
                    $(($green * 42.5)) \
                    $(($blue * 42.5))
            }
        }
    }

    # colors 232-255 are a grayscale ramp, intentionally leaving out
    # black and white
    integer gray;
    for ((gray = 0; $gray < 24; gray = $gray + 1)) {
        ((level = ($gray * 10) + 8));
        print -n -f "\x1b]4;%d;rgb:%2.2x/%2.2x/%2.2x\x1b\\" \
        $((232 + $gray)) $level $level $level;
    }

fi


# display the colors

. $HOME/.zsh/functions/spectrum

# first the system ones:
print "System colors:";
((fgcolor = 255));
for ((color = 0; $color < 8; color = $color + 1)) {
    if [[ $color -gt 0 ]]; then ((fgcolor = 232)); fi
    print -n -f "$BG[$color] $FG[$fgcolor]%3d " $color;
}
print "$FX[reset]";
((fgcolor = 232));
for ((color = 8; $color < 16; color = $color + 1)) {
    print -n -f "$BG[$color] $FG[$fgcolor]%3d " $color;
}
print "$FX[reset]";
echo

# now the color cube
print "Color cube, 6x6x6:";
((fgcolor = 255));
for ((green = 0; $green < 6; green = $green + 1)) {
    if [[ $green -gt 2 ]]; then ((fgcolor = 232)); fi
    for ((red = 0; $red < 6; red = $red + 1)) {
        for ((blue = 0; $blue < 6; blue = $blue + 1)) {
            ((color = 16 + ($red * 36) + ($green * 6) + $blue));
            print -n -f "$BG[$color] $FG[$fgcolor]%3d " $color;
        }
        print -n "$FX[reset] ";
    }
    echo
}


# now the grayscale ramp
print "Grayscale ramp:";
((fgcolor = 255));
for ((color = 232; $color < 256; color = $color + 1)) {
    if [[ $color -gt 243 ]]; then ((fgcolor = 232)); fi

    print -n -f "$BG[$color] $FG[$fgcolor]%3d " $color;
}
print "$FX[reset]";
