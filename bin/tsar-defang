#!/usr/bin/env perl6

use v6;
use JSON::Fast;

my %SUB-MAIN-OPTS = :named-anywhere;

sub MAIN(
  Str $filename = "-",             #= The file to defang (default=-)
  Bool :c($add-category)  = False, #= Add categoy to output (default=False)
  Bool :t($add-timestamp) = False, #= Add timestamp to output (default=False)
  Bool :p($add-priority)  = True,  #= Add priority to output (default=True)
) {
  my $in;
  if $filename eq '-' {
    $in = $*IN;
  }
  else {
    $in = $filename.IO.open(:r);
  }

  for $in.lines -> $line {
    my $data = from-json($line);

    my @items;
    push @items, $data<timestamp_hires> // '-' if $add-timestamp;
    push @items, $data<priority> // 'NONE'     if $add-priority;
    push @items, $data<category> // '-'        if $add-category;
    push @items, $data<message>  // '-';

    say @items.join(' ');
  }
}
