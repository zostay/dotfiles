#!/usr/bin/env perl6

use v6;
use JSON::Fast;

my %SUB-MAIN-OPTS = :named-anywhere;

my $STANDARD-FIELDS := set <
  @tag
  @timestamp
  message
  pid
  priority
  script_exec_id
  script_exec_nth
  source_host
>;

sub MAIN(
  Str $filename                 = "-",   #= The file to defang (default=-)
  Bool :c($add-category)        = False, #= Add categoy to output (default=False)
  Bool :t($add-timestamp)       = True,  #= Add timestamp to output (default=True)
  Bool :p($add-priority)        = True,  #= Add priority to output (default=True)
  Bool :n($add-non-standard)    = True,  #= Add non-standard fields (default=Treu)
) {
  my $in;
  if $filename eq '-' {
    $in = $*IN;
  }
  else {
    $in = $filename.IO.open(:r);
  }

  for $in.lines -> $line {
    try {
      my %data := from-json($line);

      my @items;
      push @items, %data<@timestamp> // '-'        if $add-timestamp;
      push @items, %data<priority>   // 'ERROR'    if $add-priority;
      push @items, %data<category>   // '-'        if $add-category;
      push @items, %data<message>    // '-';

      if $add-non-standard {
        my @ns;
        for %data.kv -> $k, $v {
          next if $k âˆˆ $STANDARD-FIELDS;
          push @ns, "$k=$v";
        }

        if @ns {
          push @items, '{', @ns.join(', '), '}';
        }
      }

      say @items.join(' ');

      CATCH {
        default {
          note $line;
        }
      }
    }
  }
}
